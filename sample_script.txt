## Advanced Math Test:
##{1d100+3} rolls a d100 and adds 3 to the answer
##{42+69} does math! 42+69=111
##{{4d8}+{8d8}} rolls multiple dice and adds them together
##{1d100-3} rolls a d100 and subtracts 3 from the answer
##{42-69} does math! 42-69=-27
##{42--69} picks a random number from 42 to 69
##{1d100*3} rolls a d100 and multiplies the answer by 3
##{1d100/3} rolls a d100 and divides the answer by 3. This will give you the accurate floating point calculation.
##{max(10, 50, 20)} returns the max value of a list of numbers.
##{min(10, 50, 20)} returns the min value of a list of numbers.
##{avg(10, 50, 20)} returns the average of a list of numbers.
##{sqrt(25)} returns the square root of a number.
##{abs(-10)} returns the absolute value of a number.
##{floor(10.9)} is rounded down.
##{ceil(10.1)} is rounded up.
##{sign(-5)} returns -1 for negative numbers, or 1 for positive numbers.

##In-line random pick: [|Happy|Sad|Angry|Confused|]
##[@PickMe] will draw a random item from the PickMe table
##[@{1d20} PickMe] will roll a 1d20 and draw that many random items from the PickMe table
##{1d[@dietype]} is a nested dice roll. It picks a random number from the dietype table, and uses it to roll a dice with that many sides.
##[@5 Table >> sort] will pick multiple items from a table and sort them numerically or alphabetically before output.
##To pick multiple items from a table and separate each pick with a comma, use implode (Put delimiters in quotes): [@4 Table >> sort >> implode ", "]
##Captialize the first letter of a word with >> proper
##lowercase a word with >> lower
##CAPITAL LETTERS with >> upper
##Use \a to put either "a" or "an" depending on what comes next.

Table: DragonHoard
<h1>The Dragon's Hoard</h1><br>You encounter: <b>[@5 Encounter >> sort]</b><hr><h3>Detailed Loot Analysis</h3><br>1. <b>Gold:</b> {round(sqrt({1d100000}))} gp<br>2. <b>Items:</b> [@{5d10} LootGen >> sort >> implode "<br>"]

Table: Encounter
{floor({1d10000/5})} [|Happy|Sad|Angry|Confused|] [@Humanoid]s
{floor({1d1000/5})} [@Humanoid] (Elite)
A hidden trap! (DC {1d20+5} to spot)

Table: Humanoid
Goblin
Kobold
Orc
Gnoll

Table: LootGen
\a [|[@Condition] [@LootTable]|[@LootTable][||| in [@Condition] condition|]|]

Table: Condition
5:pristine
5:dirty
5:mundane/unenchanted
4:broken (DC {1d20} to repair)
3:unknown (DC {1d20+10} to identify)
1:enchanted (DC {1d20+10} to identify enchantment)
1:cursed (DC {1d20+10} to remove curse)

Table: LootTable
wine skin [|(full of wine)|(full of hard liquor)|(empty)|(water)|]
bottle of hard liquor
bottle of [|fine|awful|] [|wine|beer|mead|]
small pouch of black lotus extract (about {1d100}% of the pouch is left)
religious [|medallion|icon|] worth ({1d10*10} sp)
lock of [|brunette|blonde|red|] hair
[|gold|silver|copper|steel|] [|ring|nose ring|neck chain|] with small {1d4*10}sp gem
feather from an exotic bird
[|squirrel|rabbit|racoon|jaguar|lion|tiger|] pelt
fishing hook and line
fishing net
bottle of ink ({1d100}% left) and quill
box of {1d3+1} sticks of chalk
pair of manacles
spade
[|sewing needle|sewing needle and some thread|]
spool of {10--250} ft [|chain|silk rope|linen rope|]
small vial [|(empty)|(poison DC {1d10+10})|(sleeping draught)|(hallucinogen)|(unknown)|]
small [|leather|cloth|] bag with [|dice|figurine|] game
playing cards
small [|leather|cloth|] bag of herbs ([|poison DC {1d6+9}|sleeping aid|hallucinogen|unknown|healing|stimulant|medicinal|])
[|small|large|] bag of [|nuts and raisins|vegetables|jerky|grain|hard candy|stones|seashells|]
[|small|large|] bag of [|dead|live|] [|wasps|beetles|scorpions|spiders|ants|mice|]
hunting knife
whistle
drum
flute
metal cup
block of wax
a box of {5--42} candles
list of names [|(some of them crossed off)|(and amounts owed)|]
some dirty sketches
[|clay|stone|brass|silver|gold|] figurine of a [|man|woman|horse|dragon|dog|lion|bird|phallic symbol|]






